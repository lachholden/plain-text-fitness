%import common.INT
%import common.DIGIT
%import common.LETTER
%import common.UCASE_LETTER
%import common.LCASE_LETTER
%import common.WS
%import common.WS_INLINE
%import common.ESCAPED_STRING

// General definitions
_IDENTIFIER_CHAR : LETTER | DIGIT | "-" | "_"
_WS : WS
_WS_NO_GAP : WS_INLINE | /(\r)?\n/
_WS_NEW_LINE : WS* /(\r)?\n/ WS*

// Scalar values
scalar_str : ESCAPED_STRING

DATE : DIGIT~4 "-" DIGIT~2 "-" DIGIT~2
scalar_date : DATE

// Tagging
_TAG_NAME : LETTER _IDENTIFIER_CHAR*
TAG : "#" _TAG_NAME
tag_list : TAG (_WS_NO_GAP TAG)*

// Activity key-value data
DATA_KEY : LCASE_LETTER _IDENTIFIER_CHAR*
data_key : DATA_KEY
?data_value : scalar_date | scalar_str
data_pair : data_key _WS_NO_GAP data_value
data_pairs : data_pair (_WS_NO_GAP data_pair)*

// Activities
ACTIVITY_TYPE_SEGMENT : UCASE_LETTER _IDENTIFIER_CHAR*
ACTIVITY_TYPE : ACTIVITY_TYPE_SEGMENT (":" ACTIVITY_TYPE_SEGMENT)*
activity_type : ACTIVITY_TYPE

// Statements
statement_activity : scalar_date _WS_NO_GAP activity_type [_WS_NO_GAP tag_list] [_WS_NO_GAP data_pairs]

// Document
start : (statement_activity _WS)* statement_activity?
